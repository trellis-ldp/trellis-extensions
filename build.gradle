plugins {
    id 'com.github.ben-manes.versions' version '0.21.0'
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'com.gradle.build-scan' version '2.2.1'
    id 'net.researchgate.release' version '2.7.0'
    id 'org.owasp.dependencycheck' version '4.0.2'
    id 'org.sonarqube' version '2.7'
    id 'biz.aQute.bnd.builder' version '4.1.0' apply false
    id 'com.palantir.docker' version '0.21.0' apply false
    id 'nebula.ospackage' version '6.1.2' apply false
}

ext {

    /* Dependencies */
    activationVersion = '1.1.1'
    activeMqVersion = '5.15.9'
    commonsLangVersion = '3.8.1'
    commonsRdfVersion = '0.5.0'
    dropwizardVersion = '1.3.9'
    jacksonVersion = '2.9.8'
    javaxInjectVersion = '2.5.0-b62'
    javaxAnnotationVersion = '1.3.2'
    jdbiVersion = '3.6.0'
    jmsApiVersion = '2.0.1'
    kafkaVersion = '2.1.0'
    slf4jVersion = '1.7.26'
    snakeyamlVersion = '1.24'
    tamayaVersion = '0.3-incubating'
    trellisVersion = '0.8.1-SNAPSHOT'

    /* Databases */
    mysqlVersion = '8.0.15'
    postgresVersion = '42.2.5'

    /* Testing */
    activationVersion = '1.1.1'
    apiguardianVersion = '1.0.0'
    awaitilityVersion = '3.1.6'
    checkstyleVersion = '8.12'
    commonsTextVersion = '1.6'
    guavaVersion = '27.1-jre'
    h2Version = '1.4.199'
    jaxbVersion = '2.3.0'
    jacocoVersion = '0.8.2'
    jenaVersion = '3.10.0'
    junitPlatformVersion = '1.4.1'
    junitVersion = '5.4.1'
    liquibaseVersion = '3.6.3'
    logbackVersion = '1.2.3'
    mockitoVersion = '2.25.1'
    otjPgVersion = '0.13.1'

    /* OSGi */
    projectOsgiVersion = project.version.replaceAll("-SNAPSHOT", ".SNAPSHOT")
}

allprojects { subproj ->
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.hierynomus.license'
    apply plugin: 'jacoco'

    ext {
        vendor = 'Trellis LDP'
        homepage = 'https://www.trellisldp.org'
        docURL = 'https://www.trellisldp.org/docs/trellis-ext-db/current/apidocs/'
        license = 'Apache 2'
    }

    jacoco.toolVersion = jacocoVersion

    group = 'org.trellisldp.ext'

    repositories {
        mavenLocal()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
        mavenCentral()
        jcenter()
    }

    dependencies {
        testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
        testImplementation("org.apiguardian:apiguardian-api:${apiguardianVersion}")
        testRuntime("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }

    release {
        tagTemplate = '$name-$version'
        git {
            /* Note: for maintenance branches, set this value to the appropriate branch name. */
            requireBranch = 'trellis-ext-db-0.2.x'
            signTag = true
        }
    }

    configurations.all {
        resolutionStrategy.dependencySubstitution {
            substitute module("org.apache.geronimo.specs:geronimo-annotation_1.2_spec") with module ("javax.annotation:javax.annotation-api:$javaxAnnotationVersion")
        }
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.group.startsWith('com.fasterxml.jackson.')) {
                details.useVersion jacksonVersion
                details.because 'Jackson modules should use a consistent version'
            }
        }
    }
}

subprojects { subproj ->

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    jar {
        from("$rootDir/LICENSE") {
            into "META-INF"
        }
    }

    checkstyle {
        configFile = rootProject.file('buildtools/src/main/resources/checkstyle/checkstyle.xml')
        configProperties.checkstyleConfigDir = rootProject.file('buildtools/src/main/resources/checkstyle/')
        toolVersion = checkstyleVersion
    }

    task javadocJar(type: Jar) {
        classifier 'javadoc'
        from("$rootDir/LICENSE") {
            into "META-INF"
        }
        from javadoc
    }

    task sourceJar(type: Jar) {
        classifier 'sources'
        from("$rootDir/LICENSE") {
            into "META-INF"
        }
        from sourceSets.main.allSource
    }

    task processConfig(type: Copy) {
        from('src/main/cfg') {
            include '**/*.cfg'
        }
        into 'build/cfg/main'
    }

    classes {
        classes.dependsOn processConfig
    }

    artifacts {
        archives javadocJar
        archives sourceJar
    }

    license {
        include "**/*.java"
        header rootProject.file('buildtools/src/main/resources/license/HEADER.txt')
        strictCheck true
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                pom {
                    packaging = 'jar'
                    name = 'Trellis Linked Data Server: database extension'
                    description = 'A database persistence layer for the Trellis linked data server'
                    url = "https://www.trellisldp.org"
                    inceptionYear = '2017'

                    organization {
                        name = project.vendor
                        url = project.homepage
                    }

                    developers {
                        developer {
                            id = 'acoburn'
                            name = 'Aaron Coburn'
                            email = 'acoburn (at) apache (dot) org'
                        }
                    }

                    scm {
                        connection = 'scm:git:git://github.com/trellis-ldp/trellis-ext-db.git'
                        developerConnection = 'scm:git:git@github.com/trellis-ldp/trellis-ext-db.git'
                        url = 'https://github.com/trellis-ldp/trellis-ext-db'
                        tag = 'HEAD'
                    }

                    licenses {
                        license {
                            name = 'Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0'
                            comments = 'Copyright (c) 2017-2019 Trellis LDP'
                        }
                    }
                }
                pom.withXml {
                    // eliminate test-scoped dependencies
                    asNode().dependencies.removeAll { dep -> dep.scope == "test" }
                }

                from components.java

                artifact(sourceJar) {
                    classifier = 'sources'
                }

                artifact(javadocJar) {
                    classifier = 'javadoc'
                }
            }
        }
        repositories {
            maven {
                def sonatypeUsername = project.hasProperty('ossrhUsername') ? ossrhUsername : System.getenv("SONATYPE_USERNAME") ?: ""
                def sonatypePassword = project.hasProperty('ossrhPassword') ? ossrhPassword : System.getenv("SONATYPE_PASSWORD") ?: ""
                if (version.endsWith("SNAPSHOT")) {
                    url "https://oss.sonatype.org/content/repositories/snapshots/"
                } else {
                    url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                }
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }

    tasks.withType(PublishToMavenRepository) {
        onlyIf {
            subproj.name != "trellis-db-deployment"
        }
    }
    tasks.withType(PublishToMavenLocal) {
        onlyIf {
            subproj.name != "trellis-db-deployment"
        }
    }
    task install(dependsOn: [assemble, publishToMavenLocal])
    task upload(dependsOn: [assemble, publish])

    processResources {
        outputs.upToDateWhen { false }
        filesMatching(['**/features.xml', '**/banner.txt']) {
            expand project.properties
        }
    }

    signing {
        required {
            !version.toString().endsWith('-SNAPSHOT') && tasks.withType(PublishToMavenRepository).find {
                gradle.taskGraph.hasTask it
            }
        }
        sign publishing.publications
    }

    gradle.taskGraph.whenReady( { graph ->
        tasks.withType(AbstractArchiveTask).each { archive ->
            archive.doLast {
                ant.checksum file: archive.archivePath, algorithm: 'sha-256', format: 'MD5SUM', fileext: '.sha256'
            }
        }
    })

    task docs(type: Javadoc) {
        outputs.upToDateWhen { false }
        source sourceSets.main.allJava
        classpath = files(sourceSets.main.compileClasspath)
        destinationDir = new File(projectDir, "docs/${version}")
        options {
            tags = ["apiNote:a:API Note:",
                "implSpec:a:Implementation Requirements:",
                "implNote:a:Implementation Note:"]
            links "https://docs.oracle.com/javase/8/docs/api/"
            links 'https://docs.oracle.com/javaee/7/api/'
            links "https://www.trellisldp.org/docs/trellis/${trellisVersion}/apidocs/"
            links 'https://commons.apache.org/proper/commons-lang/javadocs/api-3.8/'
            links 'https://commons.apache.org/proper/commons-rdf/apidocs/'
            links 'https://www.dropwizard.io/1.3.0/dropwizard-core/apidocs/'
        }

        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    javadoc {
        options.tags = ["apiNote:a:API Note:",
                "implSpec:a:Implementation Requirements:",
                "implNote:a:Implementation Note:"]

        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    sonarqube {
        // Only record this for jdk8 builds
        skipProject = ! JavaVersion.current().isJava8()
    }

    test {
        useJUnitPlatform()
    }

    afterReleaseBuild.dependsOn docs
    afterReleaseBuild.dependsOn publish

    jacoco {
        toolVersion = jacocoVersion
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }
}

configure(rootProject) {

    task apidocs(type: Javadoc, dependsOn: getTasksByName('docs', true)) {
        outputs.upToDateWhen { false }
        destinationDir = new File(projectDir, "docs/apidocs/${project.version}")
        title = "Trellis Linked Data Server Documentation"
        exclude '**/impl/*'
        exclude '**/*Tests.java'
        options {
            tags = ["apiNote:a:API Note:",
                "implSpec:a:Implementation Requirements:",
                "implNote:a:Implementation Note:"]
            memberLevel = JavadocMemberLevel.PUBLIC
            links "https://docs.oracle.com/javase/8/docs/api/"
            links 'https://docs.oracle.com/javaee/7/api/'
            links "https://www.trellisldp.org/docs/trellis/${trellisVersion}/apidocs/"
            links 'https://commons.apache.org/proper/commons-lang/javadocs/api-3.8/'
            links 'https://commons.apache.org/proper/commons-rdf/apidocs/'
            links 'https://www.dropwizard.io/1.3.0/dropwizard-core/apidocs/'
        }

        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }

        source subprojects.collect { project -> project.sourceSets.main.allJava }
        classpath = files(subprojects.collect { project -> project.sourceSets.main.compileClasspath })
    }

    sonarqube {
        properties {
            property "sonar.projectName", "Trellis Server: Database Extension"
            property "sonar.projectKey", "org.trellisldp:trellis-ext-db"
            property "sonar.links.homepage", "https://www.trellisldp.org"
            property "sonar.links.issue", "https://github.com/trellis-ldp/trellis-ext-db/issues"
            property "sonar.links.scm_dev", "scm:git:git@github.com:trellis-ldp/trellis-ext-db.git"
        }
    }

    // Ignore alpha, beta, milestone and release candidates
    dependencyUpdates.resolutionStrategy = {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'm'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject("Release Candidate")
                }
            }
        }
    }

    task jacocoRootReport(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
        dependsOn = subprojects.test
        additionalSourceDirs.from(files(subprojects.sourceSets.main.allSource.srcDirs))
        sourceDirectories.from(files(subprojects.sourceSets.main.allSource.srcDirs))
        classDirectories.from(files(subprojects.sourceSets.main.output))
        executionData.from(files(subprojects.findAll { it.name != 'trellis-db-deployment' }.jacocoTestReport.executionData))
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
        }
    }

    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }

    coveralls {
        sourceDirs = subprojects.sourceSets.main.allSource.srcDirs.flatten()
        jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
    }

    tasks.coveralls {
        dependsOn 'jacocoRootReport'
    }

    task getVersion {
        doLast {
            println project.version
        }
    }
}
